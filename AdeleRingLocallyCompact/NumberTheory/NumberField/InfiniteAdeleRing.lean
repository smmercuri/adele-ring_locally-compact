/-
Copyright (c) 2024 Salvatore Mercuri, Mar√≠a In√©s de Frutos-Fern√°ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri, Mar√≠a In√©s de Frutos-Fern√°ndez
-/
import Mathlib
import AdeleRingLocallyCompact.NumberTheory.NumberField.Completion
import AdeleRingLocallyCompact.Algebra.Ring.Equiv

/-!
# Infinite adele ring

This file formalises the definition of the infinite adele ring of a number field `K` as the
product of completions of `K` over its infinite places. The definition of the completions are
formalised in [AdeleRingLocallyCompact.NumberTheory.NumberField.Completion](Completion.lean).

We show that the infinite adele ring is locally compact and that it is isomorphic to the
space `‚Ñù ^ r‚ÇÅ √ó ‚ÑÇ ^ r‚ÇÇ` used in `Mathlib.NumberTheory.NumberField.mixedEmbedding`.

## Main definitions
 - `NumberField.InfiniteAdeleRing` of a number field `K` is defined as the product of
   the completions of `K` over its Archimedean places.
 - `NumberField.InfiniteAdeleRing.globalEmbedding` is the map sending `x ‚àà K` to `(x)·µ•`.
 - `NumberField.InfiniteAdeleRing.equiv_mixedSpace` is the ring isomorphism between
   the infinite adele ring of `K` and `‚Ñù ^ r‚ÇÅ √ó ‚ÑÇ ^ r‚ÇÇ`, where `(r‚ÇÅ, r‚ÇÇ)` is the signature
   of `K`.

## Main results
 - `NumberField.InfiniteAdeleRing.locallyCompactSpace` : the infinite adele ring is a
   locally compact space.
 - `NumberField.InfiniteAdeleRing.mixedEmbedding_eq_globalEmbedding_comp` : applying the
   ring isomorphism of `equiv_mixedSpace` to a globally embedded `(x)·µ•` in the infinite adele
   ring, where `x ‚àà K`, is the same as applying the embedding `K ‚Üí ‚Ñù ^ r‚ÇÅ √ó ‚ÑÇ ^ r‚ÇÇ` given by
   `NumberField.mixedEmbedding` to `x`.


## References
 * [J.W.S. Cassels, A. Fr√∂lich, *Algebraic Number Theory*][cassels1967algebraic]

## Tags
infinite adele ring, number field
-/

noncomputable section

namespace NumberField

open InfinitePlace InfinitePlace.Completion AbsoluteValue.Completion

open scoped Classical

variable (K : Type*) [Field K] [NumberField K] (v : InfinitePlace K)

/-- The infinite adele ring of a number field. -/
def InfiniteAdeleRing := (v : InfinitePlace K) ‚Üí v.completion

namespace InfiniteAdeleRing

section DerivedInstances

instance : CommRing (InfiniteAdeleRing K) := Pi.commRing

instance : Inhabited (InfiniteAdeleRing K) := ‚ü®0‚ü©

instance : Nontrivial (InfiniteAdeleRing K) :=
  (inferInstanceAs <| Nonempty (InfinitePlace K)).elim fun w => Pi.nontrivial_at w

end DerivedInstances

instance : TopologicalSpace (InfiniteAdeleRing K) := Pi.topologicalSpace

instance : TopologicalRing (InfiniteAdeleRing K) := Pi.instTopologicalRing

instance : Algebra K (InfiniteAdeleRing K) := Pi.algebra _ _

/-- The global embedding of a number field into its infinite adele ring,
sending `x ‚àà K` to `(x)·µ•`. -/
abbrev globalEmbedding : K ‚Üí+* InfiniteAdeleRing K := algebraMap K (InfiniteAdeleRing K)

@[simp]
theorem globalEmbedding_apply (x : K) : globalEmbedding K x v = x := rfl

/-- The infinite adele ring is locally compact. -/
instance locallyCompactSpace : LocallyCompactSpace (InfiniteAdeleRing K) :=
  Pi.locallyCompactSpace_of_finite

local notation "E" =>
  ({w : InfinitePlace K // IsReal w} ‚Üí ‚Ñù) √ó ({w : InfinitePlace K // IsComplex w} ‚Üí ‚ÑÇ)
/-- The ring isomorphism between the infinite adele ring of a number field and the
space `‚Ñù ^ r‚ÇÅ √ó ‚ÑÇ ^ r‚ÇÇ`, where `(r‚ÇÅ, r‚ÇÇ)` is the signature of the number field. -/
abbrev equiv_mixedSpace :
    InfiniteAdeleRing K ‚âÉ+*
      ({w : InfinitePlace K // IsReal w} ‚Üí ‚Ñù) √ó ({w : InfinitePlace K // IsComplex w} ‚Üí ‚ÑÇ) :=
  RingEquiv.trans
    (RingEquiv.piEquivPiSubtypeProd (fun (v : InfinitePlace K) => IsReal v)
      (fun (v : InfinitePlace K) => v.completion))
    (RingEquiv.prodCongr
      (RingEquiv.piCongrRight (fun ‚ü®_, hv‚ü© => Completion.equiv_real_of_isReal hv))
      (RingEquiv.trans
        (RingEquiv.piCongrRight (fun v => Completion.equiv_complex_of_isComplex
          ((not_isReal_iff_isComplex.1 v.2))))
        (RingEquiv.piCongrLeft (fun _ => ‚ÑÇ) <|
          Equiv.subtypeEquivRight (fun _ => not_isReal_iff_isComplex))))

def homeomorph_mixedSpace : InfiniteAdeleRing K ‚âÉ‚Çú
    ({w : InfinitePlace K // IsReal w} ‚Üí ‚Ñù) √ó ({w : InfinitePlace K // IsComplex w} ‚Üí ‚ÑÇ) := by
  apply Homeomorph.trans
    (Homeomorph.piEquivPiSubtypeProd (fun (v : InfinitePlace K) => IsReal v)
      (fun (v : InfinitePlace K) => v.completion))
    (Homeomorph.prodCongr
      (Homeomorph.piCongrRight
        (fun ‚ü®_, hv‚ü© => (Completion.isometryEquiv_real_of_isReal hv).toHomeomorph))
      (Homeomorph.trans
        (Homeomorph.piCongrRight (fun v => Completion.isometryEquiv_complex_of_isComplex
          ((not_isReal_iff_isComplex.1 v.2)) |>.toHomeomorph))
          (@Homeomorph.piCongrLeft _ _ (fun _ => ‚ÑÇ ) _ <|
            Equiv.subtypeEquivRight (fun _ => not_isReal_iff_isComplex))))

@[simp]
theorem equiv_mixedSpace_apply (x : InfiniteAdeleRing K) :
    equiv_mixedSpace K x =
      (fun (v : {w : InfinitePlace K // IsReal w}) => extensionEmbedding_of_isReal v.2 (x v),
       fun (v : {w : InfinitePlace K // IsComplex w}) => extensionEmbedding v.1 (x v)) := rfl

theorem equiv_mixedSpace_symm_toFun :
    ‚áë(equiv_mixedSpace K).symm = fun x : E => (fun v : InfinitePlace K => if hv : v.IsReal then
      (equiv_real_of_isReal hv).symm (x.1 ‚ü®v, hv‚ü©) else
        (equiv_complex_of_isComplex (not_isReal_iff_isComplex.1 hv)).symm <| x.2
          ‚ü®v, not_isReal_iff_isComplex.1 hv‚ü©) := rfl

/-- Transfers the global embedding of `x ‚Ü¶ (x)·µ•` of the number field `K` into its infinite adele
ring to the mixed embedding `x ‚Ü¶ (œÜ·µ¢(x))·µ¢` of `K` into the space `‚Ñù ^ r‚ÇÅ √ó ‚ÑÇ ^ r‚ÇÇ`, where
`(r‚ÇÅ, r‚ÇÇ)` is the signature of `K` and `œÜ·µ¢` are the complex embeddings of `K`. -/
theorem mixedEmbedding_eq_globalEmbedding_comp {x : K} :
    mixedEmbedding K x = equiv_mixedSpace K (globalEmbedding K x) := by
  ext ‚ü®v, hv‚ü© <;> simp only [equiv_mixedSpace_apply, globalEmbedding_apply,
    equiv_real_of_isReal, equiv_complex_of_isComplex, extensionEmbedding,
    extensionEmbedding_of_isReal, extensionEmbedding_of_comp, RingEquiv.coe_ofBijective,
    RingHom.coe_mk, MonoidHom.coe_mk, OneHom.coe_mk, UniformSpace.Completion.extensionHom]
  ¬∑ rw [UniformSpace.Completion.extension_coe
      (WithAbs.uniformInducing_of_comp <| abs_of_isReal_eq_comp hv).uniformContinuous x]
    rfl
  ¬∑ rw [UniformSpace.Completion.extension_coe
      (WithAbs.uniformInducing_of_comp <| abs_eq_comp v).uniformContinuous x]
    rfl

variable (L : Type*) [Field L] [NumberField L] [Algebra K L] [FiniteDimensional K L]

/- We show that ùî∏_K ‚äó[K] L is homeomorphic to (ùî∏_K)^[L : K]. -/
def basis_equiv : (Fin (FiniteDimensional.finrank K L) ‚Üí K) ‚âÉ‚Çó[K] L :=
  LinearEquiv.symm (Basis.equivFun (FiniteDimensional.finBasis K L))

def tensorProduct_to_pi (n : ‚Ñï) : (TensorProduct K (InfiniteAdeleRing K) (Fin n ‚Üí K))
    ‚Üí‚Çó[InfiniteAdeleRing K] (Fin n ‚Üí InfiniteAdeleRing K) :=
  (Algebra.TensorProduct.basis (InfiniteAdeleRing K) (Pi.basisFun K (Fin n))).constr
    (InfiniteAdeleRing K) (Pi.basisFun (InfiniteAdeleRing K) _)

def pi_to_tensorProduct (n : ‚Ñï) : (Fin n ‚Üí InfiniteAdeleRing K) ‚Üí‚Çó[InfiniteAdeleRing K]
    (TensorProduct K (InfiniteAdeleRing K) (Fin n ‚Üí K)) :=
  (Pi.basisFun (InfiniteAdeleRing K) _).constr K
    (Algebra.TensorProduct.basis (InfiniteAdeleRing K) (Pi.basisFun K (Fin n)))

def tensorProduct_equiv_pi' (n : ‚Ñï) : (TensorProduct K (InfiniteAdeleRing K) (Fin n ‚Üí K))
    ‚âÉ‚Çó[InfiniteAdeleRing K] (Fin n ‚Üí InfiniteAdeleRing K) where
  toFun := tensorProduct_to_pi K n
  invFun := pi_to_tensorProduct K n
  left_inv := by
    rw [Function.leftInverse_iff_comp, ‚Üê LinearMap.coe_comp,
      ‚Üê @LinearMap.id_coe (InfiniteAdeleRing K)]
    have h : pi_to_tensorProduct K n ‚àò‚Çó tensorProduct_to_pi K n = LinearMap.id := by
      apply Basis.ext (Algebra.TensorProduct.basis (InfiniteAdeleRing K) (Pi.basisFun K (Fin n)))
      intro i
      simp only [LinearMap.comp_apply, Algebra.TensorProduct.basis_apply, Pi.basisFun_apply,
        LinearMap.id_coe, id_eq, pi_to_tensorProduct, tensorProduct_to_pi,
        Basis.constr_apply_fintype, Basis.equivFun_apply,
        Algebra.TensorProduct.basis_repr_tmul, one_smul, Finsupp.mapRange_apply, Pi.basisFun_repr,
        LinearMap.stdBasis_apply', RingHom.map_ite_one_zero, Pi.basisFun_apply, ite_smul, zero_smul,
        Finset.sum_ite_eq, Finset.mem_univ, ‚ÜìreduceIte, Pi.basisFun_equivFun,
        LinearEquiv.refl_apply, Algebra.TensorProduct.basis_apply]
    rw [h]
  right_inv := by
    rw [Function.rightInverse_iff_comp, ‚Üê LinearMap.coe_comp,
      ‚Üê @LinearMap.id_coe (InfiniteAdeleRing K)]
    have h : tensorProduct_to_pi K n ‚àò‚Çó pi_to_tensorProduct K n = LinearMap.id := by
      apply Basis.ext (Pi.basisFun (InfiniteAdeleRing K) _)
      intro i
      simp only [LinearMap.comp_apply, Algebra.TensorProduct.basis_apply, Pi.basisFun_apply,
        LinearMap.id_coe, id_eq, pi_to_tensorProduct, tensorProduct_to_pi,
        Basis.constr_apply_fintype, Basis.equivFun_apply,
        Algebra.TensorProduct.basis_repr_tmul, one_smul, Finsupp.mapRange_apply, Pi.basisFun_repr,
        LinearMap.stdBasis_apply', RingHom.map_ite_one_zero, Pi.basisFun_apply, ite_smul, zero_smul,
        Finset.sum_ite_eq, Finset.mem_univ, ‚ÜìreduceIte, Pi.basisFun_equivFun,
        LinearEquiv.refl_apply, Algebra.TensorProduct.basis_apply]
    rw [h]
  map_add' _ _ := by simp only [map_add]
  map_smul' _ _ := by simp only [LinearMapClass.map_smul, RingHom.id_apply]

def tensorProduct_equiv_pi : (TensorProduct K (InfiniteAdeleRing K) L) ‚âÉ
    (Fin (FiniteDimensional.finrank K L) ‚Üí InfiniteAdeleRing K) :=
  Equiv.trans (TensorProduct.congr
      (LinearEquiv.refl K (InfiniteAdeleRing K)) (basis_equiv _ _).symm).toEquiv
    (tensorProduct_equiv_pi' _ _).toEquiv

instance : TopologicalSpace (TensorProduct K (InfiniteAdeleRing K) L) :=
  TopologicalSpace.induced (tensorProduct_equiv_pi K L) inferInstance

def tensorProduct_homeomorph_pi : (TensorProduct K (InfiniteAdeleRing K) L) ‚âÉ‚Çú
    (Fin (FiniteDimensional.finrank K L) ‚Üí InfiniteAdeleRing K) where
  toEquiv := tensorProduct_equiv_pi K L
  continuous_toFun := continuous_induced_dom
  continuous_invFun := by
    simp only [Equiv.invFun_as_coe]
    convert continuous_coinduced_rng
    rw [Equiv.coinduced_symm]
    rfl

/- We show that ‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ is homeomorphic to ‚Ñù^[K : ‚Ñö] -/

def mixedSpace_equiv_pi : E ‚âÉ (Fin (FiniteDimensional.finrank ‚Ñö K) ‚Üí ‚Ñù) := by
  apply Equiv.trans (Equiv.prodCongr (Equiv.refl _) <|
    Equiv.piCongrRight (fun _ => Complex.equivRealProd))
  have : ((v : {w : InfinitePlace K // w.IsComplex}) ‚Üí ‚Ñù √ó ‚Ñù) ‚âÉ
      (((_ : {w : InfinitePlace K // w.IsComplex}) √ó Fin 2) ‚Üí ‚Ñù) := by
    apply Equiv.symm
    apply Equiv.trans
      (Equiv.piCurry <| fun (_ : {w : InfinitePlace K // w.IsComplex}) (_ : Fin 2) => ‚Ñù)
    exact Equiv.piCongrRight (fun _ => piFinTwoEquiv _)
  apply Equiv.trans (Equiv.prodCongr (Equiv.refl _) this)
  apply Equiv.trans (Equiv.sumArrowEquivProdArrow _ _ _).symm
  apply Equiv.piCongr _ (fun _ => Equiv.refl ‚Ñù)
  apply Fintype.equivOfCardEq
  rw [‚Üê NumberField.InfinitePlace.card_add_two_mul_card_eq_rank, NrRealPlaces, NrComplexPlaces]
  rw [Fintype.card_sum, Fintype.card_sigma, Finset.sum_const, mul_comm]
  simp only [Fintype.card_fin]
  rfl

def Homeomorph.piCurry {Œ± : Type*} {Œ≤ : Œ± ‚Üí Type*} (Œ≥ : (a : Œ±) ‚Üí Œ≤ a ‚Üí Type*)
    [t : (a : Œ±) ‚Üí (b : Œ≤ a) ‚Üí TopologicalSpace (Œ≥ a b)] :
    ((x : (i : Œ±) √ó Œ≤ i) ‚Üí Œ≥ x.1 x.2) ‚âÉ‚Çú ((a : Œ±) ‚Üí (b : Œ≤ a) ‚Üí Œ≥ a b) where
  toEquiv := Equiv.piCurry _
  continuous_toFun := by
    exact continuous_pi (fun _ => continuous_pi <| fun _ => continuous_apply _)
  continuous_invFun := by
    refine continuous_pi (fun ‚ü®x, y‚ü© => ?_)
    simp [Equiv.piCurry, Sigma.uncurry]
    exact Continuous.comp' (continuous_apply _) (continuous_apply _)

variable (Œ± Œ≤ Œ≥ : Type*) [TopologicalSpace Œ≥]
def Homeomorph.sumArrowEquivProdArrow : (Œ± ‚äï Œ≤ ‚Üí Œ≥) ‚âÉ‚Çú (Œ± ‚Üí Œ≥) √ó (Œ≤ ‚Üí Œ≥) where
  toEquiv := Equiv.sumArrowEquivProdArrow _ _ _
  continuous_toFun := by
    simp [Equiv.sumArrowEquivProdArrow]
    apply And.intro
    ¬∑ apply continuous_pi
      intro i
      apply continuous_apply
    ¬∑ apply continuous_pi
      intro i
      apply continuous_apply
  continuous_invFun := by
    simp [Equiv.sumArrowEquivProdArrow]
    apply continuous_pi
    intro i
    cases i with
    | inl val =>
      simp_all only [Sum.elim_inl]
      fun_prop
    | inr val_1 =>
      simp_all only [Sum.elim_inr]
      fun_prop

def mixedSpace_homeomorph_pi : E ‚âÉ‚Çú (Fin (FiniteDimensional.finrank ‚Ñö K) ‚Üí ‚Ñù) := by
  apply Homeomorph.trans (Homeomorph.prodCongr (Homeomorph.refl _) <|
    Homeomorph.piCongrRight (fun _ => Complex.equivRealProdCLM.toHomeomorph))
  have : ((v : {w : InfinitePlace K // w.IsComplex}) ‚Üí ‚Ñù √ó ‚Ñù) ‚âÉ‚Çú
      (((_ : {w : InfinitePlace K // w.IsComplex}) √ó Fin 2) ‚Üí ‚Ñù) := by
    apply Homeomorph.symm
    apply Homeomorph.trans
      (Homeomorph.piCurry <| fun (_ : {w : InfinitePlace K // w.IsComplex}) (_ : Fin 2) => ‚Ñù)
    exact Homeomorph.piCongrRight (fun _ => Homeomorph.piFinTwo _)
  apply Homeomorph.trans (Homeomorph.prodCongr (Homeomorph.refl _) this)
  apply Homeomorph.trans (Homeomorph.sumArrowEquivProdArrow _ _ _).symm
  apply Homeomorph.piCongr _ (fun _ => Homeomorph.refl ‚Ñù)
  apply Fintype.equivOfCardEq
  rw [‚Üê NumberField.InfinitePlace.card_add_two_mul_card_eq_rank, NrRealPlaces, NrComplexPlaces]
  rw [Fintype.card_sum, Fintype.card_sigma, Finset.sum_const, mul_comm]
  simp only [Fintype.card_fin]
  rfl

/- Now put these together to show base change -/

def baseChange : (TensorProduct K (InfiniteAdeleRing K) L) ‚âÉ InfiniteAdeleRing L := by
  apply Equiv.trans (tensorProduct_equiv_pi K L)
  apply Equiv.trans <| Equiv.piCongrRight (fun _ => (equiv_mixedSpace K).toEquiv)
  apply Equiv.trans <| Equiv.piCongrRight (fun _ => (mixedSpace_equiv_pi K))
  apply Equiv.trans (Equiv.piCurry _).symm
  apply Equiv.trans ?_ (equiv_mixedSpace L).symm.toEquiv
  apply Equiv.trans ?_ (mixedSpace_equiv_pi L).symm
  apply Equiv.piCongrLeft (fun _ => ‚Ñù)
  apply Fintype.equivOfCardEq
  simp only [Fintype.card_sigma, Fintype.card_fin, Finset.sum_const, Finset.card_univ, smul_eq_mul]
  rw [mul_comm]
  rw [FiniteDimensional.finrank_mul_finrank ‚Ñö K L]

def baseChange_homeomorph : (TensorProduct K (InfiniteAdeleRing K) L) ‚âÉ‚Çú InfiniteAdeleRing L := by
  apply Homeomorph.trans (tensorProduct_homeomorph_pi K L)
  apply Homeomorph.trans <| Homeomorph.piCongrRight (fun _ => (homeomorph_mixedSpace K))
  apply Homeomorph.trans <| Homeomorph.piCongrRight (fun _ => (mixedSpace_homeomorph_pi K))
  apply Homeomorph.trans (Homeomorph.piCurry _).symm
  apply Homeomorph.trans ?_ (homeomorph_mixedSpace L).symm
  apply Homeomorph.trans ?_ (mixedSpace_homeomorph_pi L).symm
  apply @Homeomorph.piCongrLeft _ _ (fun _ => ‚Ñù) _
  apply Fintype.equivOfCardEq
  simp only [Fintype.card_sigma, Fintype.card_fin, Finset.sum_const, Finset.card_univ, smul_eq_mul]
  rw [mul_comm]
  rw [FiniteDimensional.finrank_mul_finrank ‚Ñö K L]

end InfiniteAdeleRing

end NumberField
